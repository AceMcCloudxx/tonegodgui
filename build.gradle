// Gradle build script for tonegodgui Maven artifacts

plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

ext {
    artifact = 'tonegodgui'
    version = '0.1.4'
    baseName = "$artifact-$version"
}

description = 'Tonegod\'s graphical user-interface library for jMonkeyEngine'

sourceCompatibility = '1.7'
targetCompatibility = '1.7'

dependencies {
    api 'org.jmonkeyengine:jme3-core:3.3.2-stable'
}

repositories {
    //mavenLocal()
    mavenCentral()
}

tasks.withType(JavaCompile) { // compile-time options:
    options.compilerArgs << '-Xdiags:verbose'
    options.compilerArgs << '-Xlint:unchecked'
    //options.deprecation = true
    options.encoding = 'UTF-8'
}

// custom tasks for creating source/javadoc JARs
jar {
    archiveBaseName = project.ext.baseName
}

task javadocJar(type: Jar) {
    archiveBaseName = project.ext.baseName
    classifier = 'javadoc'
    dependsOn 'javadoc'
    description 'Create a JAR of javadoc.'
    from javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    archiveBaseName = project.ext.baseName
    classifier = 'sources'
    dependsOn 'classes'
    description 'Create a JAR of sourcecode.'
    from sourceSets.main.allJava
}

build.dependsOn('pom', 'pomAsc')
task('pom', type: Copy) {
    dependsOn 'generatePomFileForMavenPublication'
    from "${buildDir}/publications/maven/pom-default.xml"
    into "${buildDir}/libs"
    rename 'pom-default.xml', baseName + '.pom'
}
task('pomAsc', type: Copy) {
    dependsOn 'signMavenPublication'
    from "${buildDir}/publications/maven/pom-default.xml.asc"
    into "${buildDir}/libs"
    rename 'pom-default.xml.asc', baseName + '.pom.asc'
}

publishing {
    publications {
        maven(MavenPublication) {
            artifact javadocJar
            artifact sourcesJar
            artifactId artifact
            from components.java
            groupId 'com.github.stephengold'
            pom {
                description = project.description
                developers {
                    developer {
                        name = 'Cris aka "t0neg0d"'
                    }
                }
                inceptionYear = '2012'
                licenses {
                    license {
                        distribution = 'repo'
                        name = 'FreeBSD License'
                        url = 'https://opensource.org/licenses/BSD-2-Clause'
                    }
                }
                name = 'com.github.stephengold:' + artifact
                scm {
                    connection = 'scm:git:git://github.com/stephengold/tonegodgui.git'
                    developerConnection = 'scm:git:ssh://github.com:stephengold/tonegodgui.git'
                    url = 'https://github.com/stephengold/tonegodgui/tree/master'
                }
                url = 'https://github.com/stephengold/tonegodgui'
            }
            version project.ext.version
        }
    }
    repositories {
        maven {
            credentials {
                username = project.hasProperty('ossrhUsername') ? ossrhUsername : 'Unknown user'
                password = project.hasProperty('ossrhPassword') ? ossrhPassword : 'Unknown password'
            }
            name = 'OSSRH'
            url = 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
        }
    }
}
publishToMavenLocal.doLast {
    println 'published to ' + baseName
}

signing {
    sign configurations.archives
    sign publishing.publications.maven
}
tasks.withType(Sign) {
    onlyIf { gradle.rootProject.hasProperty('signing.keyId') }
}

// add javadoc/source jar tasks as artifacts
artifacts {
    archives jar, sourcesJar, javadocJar
}
