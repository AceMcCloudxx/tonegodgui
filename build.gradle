// Gradle build script for tonegodgui Maven artifacts

plugins {
    id 'java-library'
    id 'maven-publish'
}

description = 'Tonegod\'s graphical user-interface library for jMonkeyEngine'
group = 'tonegod'
version = '0.1.3'

sourceCompatibility = '1.7'
targetCompatibility = '1.7'

dependencies {
    api 'org.jmonkeyengine:jme3-core:3.3.2-stable'
}

repositories {
    //mavenLocal()
    jcenter()
}

tasks.withType(JavaCompile) { // compile-time options:
    options.compilerArgs << '-Xdiags:verbose'
    options.compilerArgs << '-Xlint:unchecked'
    //options.deprecation = true
    options.encoding = 'UTF-8'
}

// custom tasks for creating source/javadoc JARs
task javadocJar(type: Jar) {
    classifier = 'javadoc'
    dependsOn 'javadoc'
    description 'Create a JAR of javadoc.'
    from javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    dependsOn 'classes'
    description 'Create a JAR of sourcecode.'
    from sourceSets.main.allJava
}

build.dependsOn('pom')
task('pom', type: Copy) {
    dependsOn 'generatePomFileForMavenPublication'
    from "${buildDir}/publications/maven/pom-default.xml"
    into "${buildDir}/libs"
    rename '.*', 'tonegodgui-0.1.3.pom'
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId 'tonegodgui'
            from components.java
            pom {
                licenses {
                    license {
                        distribution = 'repo'
                        name = 'FreeBSD License'
                        url = 'https://opensource.org/licenses/BSD-2-Clause'
                    }
                }
            }
            version '0.1.3'
        }
    }
}

// add javadoc/source jar tasks as artifacts
artifacts {
    archives jar, sourcesJar, javadocJar
}
