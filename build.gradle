plugins {
    id 'java-library'
    id 'maven'
}

description = 'Tonegod\'s graphical user-interface library for jMonkeyEngine'
group = 'tonegod'
version = '0.1.2'

sourceCompatibility = '1.7'
targetCompatibility = '1.7'

gradle.projectsEvaluated {
    tasks.withType(JavaCompile) { // compile-time options:
        options.compilerArgs << '-Xdiags:verbose'
        options.compilerArgs << '-Xlint:unchecked'
        //options.deprecation = true
        options.encoding = 'UTF-8'
    }
}

repositories {
    //mavenLocal()
    jcenter()
}

dependencies {
    api 'org.jmonkeyengine:jme3-core:3.2.4-stable'
}

// custom tasks for creating source/javadoc JARs
task sourcesJar(type: Jar) {
    classifier = 'sources'
    dependsOn 'classes'
    description 'Create a JAR of source files.'
    from sourceSets.main.allJava
}
task javadocJar(type: Jar) {
    classifier = 'javadoc'
    dependsOn 'javadoc'
    description 'Create a JAR of javadoc.'
    from javadoc.destinationDir
}

build.dependsOn('pom')
task pom {
    doLast {
        pom {
            project {
                artifactId 'tonegodgui'
                licenses {
                    license {
                        distribution 'repo'
                        name 'FreeBSD License'
                        url 'https://opensource.org/licenses/BSD-2-Clause'
                    }
                }
                version '0.1.2'
            }
        }.writeTo("${buildDir}/libs/tonegodgui-0.1.2.pom")
    }
}

// add javadoc/source jar tasks as artifacts
artifacts {
    archives jar, sourcesJar, javadocJar
}
