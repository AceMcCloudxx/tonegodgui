apply plugin: 'java'
apply plugin: 'maven'

description = 'Tonegod\'s graphical user-interface library for jMonkeyEngine'
group = 'tonegod'
version = '0.1.2'

// select one source-code option
sourceCompatibility = '1.7'
//sourceCompatibility = '1.8'

gradle.projectsEvaluated {
    tasks.withType(JavaCompile) { // compile-time options:
        options.compilerArgs << '-Xdiags:verbose'
        options.compilerArgs << '-Xlint:unchecked'
        //options.deprecation = true
        options.encoding = 'UTF-8'
    }
}

repositories {
    //maven { url 'https://dl.bintray.com/jmonkeyengine/com.jme3' }
    maven { url 'https://jcenter.bintray.com' }
}

dependencies {
    //compile 'com.jme3:jme3-core:3.0.10'
    //compile 'org.jmonkeyengine:jme3-core:3.1.0-stable'
    //compile 'org.jmonkeyengine:jme3-core:3.2.0-stable'
    //compile 'org.jmonkeyengine:jme3-core:3.2.1-stable'
    //compile 'org.jmonkeyengine:jme3-core:3.2.2-stable'
    compile 'org.jmonkeyengine:jme3-core:3.2.3-stable'
}

// custom tasks for creating source/javadoc jars
task sourcesJar(type: Jar, dependsOn: classes, description: 'Create a jar of source files.') {
    classifier = 'sources'
    from sourceSets.main.allJava
}
task javadocJar(type: Jar, dependsOn: javadoc, description: 'Create a jar of javadoc.') {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

build.dependsOn('pom')
task pom {
    doLast {
        pom {
            project {
                artifactId 'tonegodgui'
                licenses {
                    license {
                        distribution 'repo'
                        name 'FreeBSD License'
                        url 'https://opensource.org/licenses/BSD-2-Clause'
                    }
                }
                version '0.1.2'
            }
        }.writeTo("${buildDir}/libs/tonegodgui-0.1.2.pom")
    }
}

// add javadoc/source jar tasks as artifacts
artifacts {
    archives sourcesJar, javadocJar
}
